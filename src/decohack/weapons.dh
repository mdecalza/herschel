/*****************************************************************************
 * DECOHack Patch File
 * Weapons
 * Matthew "Moustachio" DeCalzadilla 2025
 ****************************************************************************/

#include <dsdhacked>

misc
{
	initialbullets 75
}

ammo 0 "Bullets"
{
	max 250
}

weapon 0 "Fist"
{
	// MBF21 Flags (MBF21 feature set only)
	+NOAUTOSWITCHTO
	
	States
	{
		Deselect:
			PUNG A 1 Offset(1, 92)
			Goto DeselectLoop
		DeselectLoop:
			PUNG A 1 A_Lower
			Loop
		Select:
			PUNG A 1 Offset(1, 52) A_Raise
			PUNG A 1 Offset(1, 32)
			Goto Ready
		Fire:
			PUNG C 2
			PUNG DD 0 A_Punch
			PUNG D 2 A_Punch
			PUNG DC 3
			PUNG B 2
			Goto Ready
	}
}

weapon 1 "Pistol"
{
	// MBF21 Flags (MBF21 feature set only)
	-AUTOSWITCHFROM
	
	States
	{
		Deselect:
			PISG A 1 Offset(1, 92)
			Goto DeselectLoop
		DeselectLoop:
			PISG A 1 A_Lower
			Loop
		Select:
			PISG A 1 Offset(1, 52) A_Raise
			PISG A 1 Offset(1, 32)
			Goto Ready
		Fire:
			PISG B 3 A_FirePistol
			PISG C 2
			Goto Ready
		Flash:
			PISF A 3 Bright A_Light1
			TNT1 A 3 A_Light1
			Goto LightDone
	}
}

weapon 2 "Shotgun"
{	
	States
	{
		Deselect:
			SHTG A 1 Offset(1, 92)
			Goto DeselectLoop
		DeselectLoop:
			SHTG A 1 A_Lower
			Loop
		Select:
			SHTG A 1 Offset(1, 52) A_Raise
			SHTG A 1 Offset(1, 32)
			Goto Ready
		Fire:
			SHTG A 2
			SHTG A 4 A_FireShotgun
			SHTG B 1
			SHTG C 2
			SHTG D 2
			SHTG C 2
			SHTG B 3
			SHTG A 3
			SHTG A 3 A_ReFire
			Goto Ready
		Flash:
			SHTF A 2 Bright A_Light1
			SHTF B 2 Bright A_Light2
			Goto LightDone
	}
}

weapon 3 "Chaingun"
{
	States
	{
		Deselect:
			CHGG A 1 Offset(1, 92)
			Goto DeselectLoop
		DeselectLoop:
			CHGG A 1 A_Lower
			Loop
		Select:
			CHGG A 1 Offset(1, 52) A_Raise
			CHGG A 1 Offset(1, 32)
			Goto Ready
		Fire:
			CHGG A 1 Bright {
				A_WeaponBulletAttack(5.5)
				A_ConsumeAmmo
				A_WeaponSound("PISTOL")
				A_GunFlashTo(Flash)
			}
			CHGG B 1 Bright {
				A_WeaponBulletAttack(5.5)
				A_ConsumeAmmo
			}
		ChaingunWindDown:
			CHGG B 0 A_ReFire
			CHGG AB 1
			CHGG AB 2
			CHGG A 3
			CHGG B 4
			CHGG A 5	
			CHGG B 6
			CHGG A 7
			CHGG B 8
			Goto Ready
		Flash:
			CHGF AB 2 A_Light2
			Goto LightDone
	}
}

// Manually define chaingun fire states maintain flash offset
state protect 52 to 53

weapon 4 "Rocket launcher"
{
	States
	{
		Deselect:
			MISG A 1 Offset(1, 92)
			Goto DeselectLoop
		DeselectLoop:
			MISG A 1 A_Lower
			Loop
		Select:
			MISG A 1 Offset(1, 52) A_Raise
			MISG A 1 Offset(1, 32)
			Goto Ready
	}
}

weapon 5 "Plasma rifle"
{
	States
	{
		Deselect:
			PLSG A 1 Offset(1, 92)
			Goto DeselectLoop
		DeselectLoop:
			PLSG A 1 A_Lower
			Loop
		Select:
			PLSG A 1 Offset(1, 52) A_Raise
			PLSG A 1 Offset(1, 32)
			Goto Ready
		Fire:
			PLSG A 3 Bright A_FirePlasma
			PLSG A 0 A_ReFire
			Goto Ready
	}
}

weapon 6 "BFG 9000"
{
	States
	{
		Deselect:
			BFGG A 1 Offset(1, 92)
			Goto DeselectLoop
		DeselectLoop:
			BFGG A 1 A_Lower
			Loop
		Select:
			BFGG A 1 Offset(1, 52) A_Raise
			BFGG A 1 Offset(1, 32)
			Goto Ready
	}
}

weapon 7 "Chainsaw"
{
	// MBF21 Flags (MBF21 feature set only)
	+NOAUTOSWITCHTO
	+FLEEMELEE

	States
	{
		Ready:
			SAWG CD 2 A_WeaponReady
			Loop
		Deselect:
			SAWG A 1 Offset(1, 92)
			Goto DeselectLoop
		DeselectLoop:
			SAWG A 1 A_Lower
			Loop
		Select:
			SAWG A 1 Offset(1, 52) A_Raise
			SAWG A 1 Offset(1, 32)
			Goto Ready
		Fire:
			SAWG AB 2 {
				A_Saw
				A_Saw
			}
			SAWG B 0 A_ReFire
			Goto Ready
	}
}

weapon 8 "Super-shotgun"
{
	States
	{
		Deselect:
			SHT2 A 1 Offset(1, 92)
			Goto DeselectLoop
		DeselectLoop:
			SHT2 A 1 A_Lower
			Loop
		Select:
			SHT2 A 1 Offset(1, 52) A_Raise
			SHT2 A 1 Offset(1, 32)
			Goto Ready
		Flash:
			SHT2 I 4 Bright A_Light1
			SHT2 J 3 Bright A_Light2
			Goto LightDone
	}
}